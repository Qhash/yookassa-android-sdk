/*
 * The MIT License (MIT)
 * Copyright © 2020 NBCO YooMoney LLC
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy of this software and
 * associated documentation files (the “Software”), to deal in the Software without restriction, including
 * without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies
 * of the Software, and to permit persons to whom the Software is furnished to do so, subject to the
 * following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all copies or substantial
 * portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED “AS IS”, WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED,
 * INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR
 * PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE
 * LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT
 * OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR
 * OTHER DEALINGS IN THE SOFTWARE.
 */

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply from: 'jacoco.gradle'
applyFromFileIfExists('buildInternal.gradle')

ext {
    coverageSourceDirs = 'src/test/java'
}

android {
    compileSdkVersion target_sdk_version
    buildToolsVersion "30.0.3"

    resourcePrefix 'ym_'

    defaultConfig {
        minSdkVersion 21
        versionCode project.ext.versionCode
        versionName project.ext.versionName

        multiDexEnabled true
        targetSdkVersion rootProject.ext.target_sdk_version

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        buildConfigField "String", "THREAT_METRIX_ORIG_ID", "${optionalProperty('threatMetrixOrigId')}"
        buildConfigField "String", "THREAT_METRIX_FP_SERVER", "${optionalProperty('threatMetrixFPServer')}"
        buildConfigField "String", "THREAT_METRIX_CERTIFICATE_HASHES", "${optionalProperty('threatMetrixCertificateHashes')}"
        buildConfigField "String", "APP_METRICA_KEY", "${optionalProperty('metricaKey')}"

        buildConfigField "Integer", "VERSION_CODE", "${project.ext.versionCode}"
        buildConfigField "String", "VERSION_NAME", "\"${project.ext.versionName}\""

        vectorDrawables.useSupportLibrary = true

        resValue "string", "ym_app_scheme", ""
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            consumerProguardFiles 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    useLibrary 'org.apache.http.legacy'

    libraryVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${generateFileName(project, variant)}.aar"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.google.android.gms:play-services-wallet:18.1.2'
    implementation "androidx.appcompat:appcompat:$appcompat"
    implementation "com.google.android.material:material:$material"
    implementation "androidx.recyclerview:recyclerview:$recyclerview"
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayout"
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
    implementation 'com.yandex.android:mobmetricalib:3.16.2'
    implementation 'ru.tinkoff.decoro:decoro:1.5.0'
    implementation 'androidx.fragment:fragment-ktx:1.3.5'

    implementation 'commons-io:commons-io:2.5'
    implementation 'commons-codec:commons-codec:1.15'
    implementation "ru.yoomoney.sdk:march:$march_lib_version"
    implementation "ru.yoomoney.sdk.gui:ui-lib:$ui_lib_version"
    compileOnly fileTree(dir: "libs", include: ["profiling-tool.aar"])
    implementation ("ru.yoomoney.sdk.auth:auth:$yoo_sdk_auth_version")

    def daggerVersion = "2.35.1"
    implementation "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"

    testImplementation "junit:junit:$junit"
    testImplementation 'androidx.test:core:1.3.0'
    testImplementation "org.mockito:mockito-core:$mockito_version"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.0.0"
    testImplementation 'org.robolectric:robolectric:4.0.1'
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.7"
    testImplementation 'org.assertj:assertj-core:3.15.0'
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.7"
}

task copyTestClasses(type: Copy) {
    from "build/tmp/kotlin-classes/devDebugUnitTest"
    into "build/intermediates/classes/devDebug"
    exclude '*/META-INF/library_devDebug.kotlin_module'
}